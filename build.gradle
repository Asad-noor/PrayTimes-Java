buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'

def currentVersion = "0.9.3.1";
def currentVersionInfo = "prerelease"

group 'org.metinkale'
version currentVersion
sourceCompatibility = 1.7

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

jar.dependsOn sourcesJar
jar.dependsOn javadocJar

bintray {
    user = bintray_user
    key = bintray_apikey
    pkg {
        repo = 'maven'
        name = 'PrayTimes-Java'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/metinkale38/praytimes-java.git'
        publications = ['MyPublication']
        version {
            name = currentVersion
            desc = currentVersionInfo
            gpg {
                sign = true
                passphrase = bintray_gpgpass
            }
        }
    }
}


def pomConfig = {
    licenses {
        license {
            name "GNU LESSER GENERAL PUBLIC LICENSE, Version 3.0"
            url "https://www.gnu.org/licenses/lgpl-3.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "metinkale38"
            name "Metin Kale"
            email "metinkale38@gmail.com"
        }
    }

    scm {
        url "https://github.com/metinkale38/PrayTimes-Java.git"
    }
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.metinkale'
            artifactId 'praytimes'
            version currentVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Pray Times provides a set of handy functions to calculate prayer times for any location around the world, based on a variety of calculation methods currently used in Muslim communities.')
                root.appendNode('name', 'PrayTimes-Java')
                root.appendNode('url', 'https://github.com/metinkale38/PrayTimes-Java')
                root.children().last() + pomConfig
            }
        }
    }
}

